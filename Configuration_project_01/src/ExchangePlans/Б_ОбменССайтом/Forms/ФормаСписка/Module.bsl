#Область ОбработчикиСобытийФормы
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ОбновлениеМодуляОбмена();
	
КонецПроцедуры

&НаСервере
Процедура ОбновлениеМодуляОбмена()
	
	Если Б_ОбменССайтомСервер.Версия() <> Константы.Б_НомерВерсииМодуляОбменаССайтом.Получить() тогда	
		
		Попытка
			ОбновлениеУспешно = Б_ОбменССайтомСервер.ПроверкаОбновленияДанныхМодуляОбменаССайтом(Неопределено);
		Исключение
			Сообщить(ОписаниеОшибки());	
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗавершенСеансОбменаССайтом" Тогда
		
		Элементы.Список.Обновить();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

////////////////////////////////////////////////////////////////////////////////
&НаКлиенте
Процедура ЭкспортНастроекВФайл(Команда)
	
	Если Элементы.Список.ТекущиеДанные <> Неопределено тогда
		
		КодНастройки = Элементы.Список.ТекущиеДанные.Код;
		
		Если КодНастройки = "" тогда
			Возврат;
		КонецЕсли;
		
		АдресДоНастроек = ЭкспортНастроекВФайлНаСервере(КодНастройки);
		
#ЕСЛИ ВебКлиент тогда
		ПолучитьФайл(АдресДоНастроек,  Элементы.Список.ТекущиеДанные.Наименование, Истина);
#ИНАЧЕ
		ПолучитьФайл(АдресДоНастроек, , Истина);
#КОНЕЦЕСЛИ		
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЭкспортНастроекВФайлНаСервере(КодНастройки)
	
	НайденныйПлан = ПланыОбмена.Б_ОбменССайтом.НайтиПоКоду(КодНастройки);
	
	Если НЕ НайденныйПлан.Пустая() тогда
		
		Результат = Новый Структура;
		
		Для каждого ТекСтрока из Метаданные.ПланыОбмена.Б_ОбменССайтом.Реквизиты Цикл
			
			Результат.Вставить(ТекСтрока.Имя, НайденныйПлан[ТекСтрока.Имя]); 
			
		КонецЦикла;
		
	КонецЕсли;
	
	ПутьКФайлу = ПолучитьИмяВременногоФайла();
	ЗначениеВФайл(ПутьКФайлу, Результат);
	
	ДвоичныеДанные = Новый ДвоичныеДанные(ПутьКФайлу);
	
	Возврат ПоместитьВоВременноеХранилище(ДвоичныеДанные);
	
КонецФункции

&НаКлиенте
Процедура ИмпортНастрокеИзФайла(Команда)
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	Диалог.Заголовок = НСтр("ru = 'Укажите файл с настройками'");
	
	ОбработкаОкончанияЗагрузки = Новый ОписаниеОповещения("ИмпортНастрокеИзФайлаЗавершениеЗагрузки", ЭтотОбъект, Диалог); 
	НачатьПомещениеФайла(ОбработкаОкончанияЗагрузки, , Диалог, Истина, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ИмпортНастрокеИзФайлаЗавершениеЗагрузки(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	
	Если Результат Тогда
		
		ИмяФайла = Б_ОбщиеПроцедурыИФункцииСервер.ПолучитьАдресВременногоФайлаСервер(Адрес, ВыбранноеИмяФайла); 
		 
		лСтруктура = новый Структура;
		лСтруктура.Вставить("ПолноеИмяФайла", ИмяФайла);
		
		Подсказка = "Введите  название будущей настройки";
		Оповещение = Новый ОписаниеОповещения("ПослеВводаНазванияНастройки", ЭтотОбъект, лСтруктура);
		ПоказатьВводСтроки(Оповещение, "", Подсказка, 0, Истина);

	Иначе
		Сообщить("Файл не был указан.");
	КонецЕсли 
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаНазванияНастройки(Строка, пПараметры) Экспорт
	
	Если НЕ Строка = Неопределено Тогда
		ИмпортНастрокеИзФайлаНаСервере(Строка, пПараметры);
	КонецЕсли;
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура ИмпортНастрокеИзФайлаНаСервере(НазваниеНастройки, пПараметры)
	
	СохраненныеЗначения = ЗначениеИзФайла(пПараметры.ПолноеИмяФайла);
	
	НовыйПлан = ПланыОбмена.Б_ОбменССайтом.СоздатьУзел();
	НовыйПлан.УстановитьНовыйКод();
	НовыйПлан.Наименование = НазваниеНастройки; 
	
	Для каждого ТекСтрока из СохраненныеЗначения Цикл
		
		Если НовыйПлан.Метаданные().Реквизиты.Найти(ТекСтрока.Ключ) <> Неопределено тогда
			НовыйПлан[ТекСтрока.Ключ] = ТекСтрока.Значение;	
		Иначе
			Сообщить("Не найдена настройка " + ТекСтрока.Ключ);
		КонецЕсли;
		
	КонецЦикла;
	
	НовыйПлан.Записать();
	
КонецПроцедуры


#КонецОбласти


#Область ПрочиеПроцедурыИФункции
////////////////////////////////////////////////////////////////////////////////
// ПРОЧИЕ

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
		
	Если Поле.Имя = "Ошибка" Тогда
		Если ЗначениеЗаполнено(Элемент.ТекущиеДанные.Ошибка) тогда
			СтандартнаяОбработка = Ложь;
			Оповещение = Новый ОписаниеОповещения("ПослеВводаСтроки", ЭтаФорма);
			ПоказатьВводСтроки(Оповещение, Элемент.ТекущиеДанные.Ошибка, "Информация о ошибке во время выпонения обмена с сайтом",,Истина);
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаСтроки(Строка, Параметры) Экспорт
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаОВерсииМодуля(Результат, Параметры) Экспорт
	
КонецПроцедуры

#КонецОбласти
