
Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", 		ДокументСсылка);
	Запрос.УстановитьПараметр("ДатаСреза", 		ДокументСсылка.Дата);
	Запрос.УстановитьПараметр("БизнесРегион", 	ДокументСсылка.БизнесРегион);
	
КонецПроцедуры

Функция ПолучитьСписокПунктовВыдачиПоОрганизации(Организация) Экспорт	
	
	Запрос	= Новый Запрос;
	ТекстЗапроса	= "
	
		|ВЫБРАТЬ
		|	АЭС_ПунктыВыдачи.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.АЭС_ПунктыВыдачи КАК АЭС_ПунктыВыдачи
		|ГДЕ
		|	АЭС_ПунктыВыдачи.Организация = &Организация
		|	И НЕ АЭС_ПунктыВыдачи.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	АЭС_ПунктыВыдачи.Наименование";
	
	Запрос.Текст	= ТекстЗапроса;
	
	Запрос.УстановитьПараметр("Организация",Организация);
		
	ВыборкаПунктыВыдачи	= Запрос.Выполнить().Выгрузить();
	
	Возврат ВыборкаПунктыВыдачи;
		
КонецФункции // Функция ПолучитьСписокПунктовВыдачиПоОрганизации(Организация) Экспорт

Функция ПолучитьСписокСкладовПоОрганизации(БизнесРегион) Экспорт	
	
	Запрос	= Новый Запрос;
	ТекстЗапроса	= "
	
		|ВЫБРАТЬ
		|	Склады.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Склады КАК Склады
		|ГДЕ
		|	Склады.БизнесРегион = &БизнесРегион
		|	И НЕ Склады.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	Склады.Ссылка.Наименование";
	
	Запрос.Текст	= ТекстЗапроса;
	
	Запрос.УстановитьПараметр("БизнесРегион",БизнесРегион);
		
	ВыборкаПунктыВыдачи	= Запрос.Выполнить().Выгрузить();
	
	Возврат ВыборкаПунктыВыдачи;
		
КонецФункции // Функция ПолучитьСписокПунктовВыдачиПоОрганизации(Организация) Экспорт

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	ТекстыЗапроса = Новый СписокЗначений;
	Если ДокументСсылка.ВидМатрицы	= Перечисления.АЭС_ВидМатрицыРезервирования.ПоПунктамВыдачи Тогда
		ТекстЗапросаТаблицаПунктовВыдачи(Запрос, ТекстыЗапроса, Регистры);
	Иначе
		ТекстЗапросаТаблицаПунктовВыдачиПоСкладам(Запрос, ТекстыЗапроса, Регистры);
	КонецЕсли; // Если ДокументСсылка.ВидМатрицы	= Перечисления.АЭС_ВидМатрицыРезервирования.ПоПунктамВыдачи Тогда
	
	ТекстЗапросаАктуальныеПунктыВыдачи(Запрос, ТекстыЗапроса, Регистры);
	
	ПроведениеСерверУТ.ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры // Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт

Функция ТекстЗапросаТаблицаПунктовВыдачи(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	ИмяРегистра = "АЭС_ПриоритетыСкладов";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
		
	ТекстЗапроса ="
		|ВЫБРАТЬ
		|	Матрица.ПунктКуда 	КАК ПунктВыдачи,
		|	Матрица.Ссылка.Дата КАК ПЕРИОД,
		|	Матрица.Приоритет 	КАК Приоритет,
		|	Склады.Склад 		КАК Склад	
		|ИЗ
		|	Документ.АЭС_МатрицаСкладовПоПриоритетам.ТаблицаПриоритетов КАК Матрица
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.АЭС_ПунктыВыдачи.Склады КАК Склады
		|		ПО (Матрица.ПунктОткуда = Склады.Ссылка)
		|ГДЕ
		|	Матрица.Ссылка = &Ссылка";

	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаАктуальныеПунктыВыдачи(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	ИмяРегистра = "АЭС_АктуальныеПриоритеты";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
		
	ТекстЗапроса ="
		|ВЫБРАТЬ
		|	Матрица.Организация	КАК Организация,
		|	Матрица.Ссылка.Дата КАК ПЕРИОД,
		|	Матрица.Ссылка		КАК Документ
		|ИЗ
		|	Документ.АЭС_МатрицаСкладовПоПриоритетам КАК Матрица
		|ГДЕ
		|	Матрица.Ссылка = &Ссылка";

	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПунктовВыдачиПоСкладам(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	ИмяРегистра = "АЭС_ПриоритетыСкладов";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
		
	ТекстЗапроса ="
		|ВЫБРАТЬ
		|	Матрица.ПунктКуда КАК ПунктВыдачи,
		|	Матрица.Ссылка.Дата КАК ПЕРИОД,
		|	Матрица.Приоритет КАК Приоритет,
		|	Матрица.ПунктОткуда КАК Склад
		|ИЗ
		|	Документ.АЭС_МатрицаСкладовПоПриоритетам.ПриоритетыПоСкладам КАК Матрица
		|ГДЕ
		|	Матрица.Ссылка = &Ссылка";

	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции
