#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ПометкаУдаления = ПометкаУдаления(ПараметрКоманды[0]);
	УстановитьПометкуУдаления(ПараметрКоманды, Не ПометкаУдаления);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПометкаУдаления(Знач СсылкаНаОбъект)
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаОбъект, "ПометкаУдаления");
КонецФункции

&НаКлиенте
Процедура УстановитьПометкуУдаления(НаборДокументов, ПометкаУдаления)
	
	Контекст = Новый Структура;
	Контекст.Вставить("НаборДокументов", НаборДокументов);
	Контекст.Вставить("ПометкаУдаления", ПометкаУдаления);
	Контекст.Вставить("КонтекстОперации", ЭлектронноеВзаимодействиеСлужебныйКлиент.НовыйКонтекстОперации());
	
	Оповещение = Новый ОписаниеОповещения("УстановитьПометкуУдаленияПослеВопроса", ЭтотОбъект, Контекст);
	
	ТекстВопроса = ?(ПометкаУдаления, НСтр("ru = 'Пометить на удаление?'"), НСтр("ru = 'Снять пометку на удаление?'"));
	
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПометкуУдаленияПослеВопроса(Ответ, Контекст) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	КонтекстОперации = Контекст.КонтекстОперации;
	
	УстановитьПометкуУдаленияНаСервере(Контекст.НаборДокументов, Контекст.ПометкаУдаления, КонтекстОперации);
	
	Если КонтекстОперации.Диагностика.Ошибки.Количество() > 0 Тогда
		КонтекстОперации.ЗаголовокОперации = НСтр("ru = 'Установка пометки удаления'");
		ЭлектронноеВзаимодействиеОбработкаОшибокКлиент.ОбработатьОшибки(КонтекстОперации);
		Возврат;
	КонецЕсли;
	
	ТекстОповещения = ?(Контекст.ПометкаУдаления, НСтр("ru = 'Пометка удаления установлена'"),
		НСтр("ru = 'Пометка удаления снята'"));
	
	КоличествоДокументов = Контекст.НаборДокументов.Количество();
	Если КоличествоДокументов > 1 Тогда
		Пояснение = СтрШаблон(НСтр("ru = 'Электронные документы (%1)'"), КоличествоДокументов);
		ПоказатьОповещениеПользователя(ТекстОповещения,, Пояснение, БиблиотекаКартинок.ДиалогИнформация);
		ОбщегоНазначенияКлиент.ОповеститьОбИзмененииОбъектов(Контекст.НаборДокументов);
	Иначе
		ЭлектронныйДокумент = Контекст.НаборДокументов[0];
		НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(ЭлектронныйДокумент);
		ПоказатьОповещениеПользователя(ТекстОповещения, НавигационнаяСсылка, ЭлектронныйДокумент, БиблиотекаКартинок.ДиалогИнформация);
		ОбщегоНазначенияКлиент.ОповеститьОбИзмененииОбъекта(ЭлектронныйДокумент);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПометкуУдаленияНаСервере(Знач НаборДокументов, Знач ПометкаУдаления, КонтекстОперации)
	Для Каждого ЭлектронныйДокумент Из НаборДокументов Цикл
		ОбменСКонтрагентамиСлужебный.УстановитьПометкуУдаления(ЭлектронныйДокумент, ПометкаУдаления, КонтекстОперации);
	КонецЦикла;
КонецПроцедуры

#КонецОбласти