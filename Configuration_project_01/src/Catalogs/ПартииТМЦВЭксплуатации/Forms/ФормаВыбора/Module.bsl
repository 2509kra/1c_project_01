
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Параметры.Свойство("ОграничениеВыбора") 
		ИЛИ НЕ Параметры.ОграничениеВыбора Тогда
		Элементы.ОтборВЭксплуатации.Видимость = Ложь;
	КонецЕсли;

	УстановитьСвойстваДинамическогоСписка();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборВЭксплуатацииПриИзменении(Элемент)
	
	УстановитьОтборы(ЭтотОбъект);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьСвойстваДинамическогоСписка()

	Если НЕ Параметры.Свойство("ОграничениеВыбора") 
		ИЛИ НЕ Параметры.ОграничениеВыбора Тогда
		Возврат;
	КонецЕсли;
	
	ОтборВЭксплуатации = 1;
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("Дата", Дата(1, 1, 1, 0, 0, 0));
	ПараметрыЗапроса.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	ПараметрыЗапроса.Вставить("Подразделение", Справочники.СтруктураПредприятия.ПустаяСсылка());
	ПараметрыЗапроса.Вставить("ФизическоеЛицо", Справочники.ФизическиеЛица.ПустаяСсылка());
	ПараметрыЗапроса.Вставить("Номенклатура", Справочники.Номенклатура.ПустаяСсылка());
	ПараметрыЗапроса.Вставить("Характеристика", Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());

	Если Параметры.Свойство("ТекущийРегистратор") Тогда
		
		ПараметрыЗапроса.Вставить("ТекущийРегистратор", Неопределено);
	
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПартииТМЦВЭксплуатацииПереопределяемый.Ссылка,
		|	ПартииТМЦВЭксплуатацииПереопределяемый.ПометкаУдаления,
		|	ПартииТМЦВЭксплуатацииПереопределяемый.Наименование,
		|	ПартииТМЦВЭксплуатацииПереопределяемый.Документ,
		|	ПартииТМЦВЭксплуатацииПереопределяемый.Дата,
		|	ПартииТМЦВЭксплуатацииПереопределяемый.КатегорияЭксплуатации,
		|	ПартииТМЦВЭксплуатацииПереопределяемый.ИнвентарныйУчет,
		|	ПартииТМЦВЭксплуатацииПереопределяемый.ИнвентарныйНомер,
		|	ПартииТМЦВЭксплуатацииПереопределяемый.СрокЭксплуатации,
		|	ПартииТМЦВЭксплуатацииПереопределяемый.ДатаЗавершенияЭксплуатации,
		|	ПартииТМЦВЭксплуатацииПереопределяемый.СпособПогашенияСтоимостиБУ,
		|	ПартииТМЦВЭксплуатацииПереопределяемый.ЕдиницаИзмеренияНаработки,
		|	ПартииТМЦВЭксплуатацииПереопределяемый.ОбъемНаработки,
		|	ПартииТМЦВЭксплуатацииПереопределяемый.СтатьяРасходов,
		|	ПартииТМЦВЭксплуатацииПереопределяемый.АналитикаРасходов,
		|	ПартииТМЦВЭксплуатацииПереопределяемый.ФизическоеЛицо,
		|	ПартииТМЦВЭксплуатацииПереопределяемый.СпособПогашенияСтоимостиНУ,
		|	ПартииТМЦВЭксплуатацииПереопределяемый.НаправлениеДеятельности,
		|	ПартииТМЦВЭксплуатацииПереопределяемый.Предопределенный,
		|	ПартииТМЦВЭксплуатацииПереопределяемый.ИмяПредопределенныхДанных
		|ИЗ
		|	Справочник.ПартииТМЦВЭксплуатации КАК ПартииТМЦВЭксплуатацииПереопределяемый
		|ГДЕ
		|	НЕ ПартииТМЦВЭксплуатацииПереопределяемый.ПометкаУдаления
		|
		|	И (НЕ &ОтборДата
		|		ИЛИ ПартииТМЦВЭксплуатацииПереопределяемый.ДатаЗавершенияЭксплуатации > &Дата
		|		ИЛИ ПартииТМЦВЭксплуатацииПереопределяемый.ДатаЗавершенияЭксплуатации = ДАТАВРЕМЯ(1, 1, 1))
		|
		|	И ПартииТМЦВЭксплуатацииПереопределяемый.Ссылка В
		|		(ВЫБРАТЬ
		|			ВложенныйЗапрос.Ссылка 
		|		ИЗ (ВЫБРАТЬ
		|				ТМЦВЭксплуатации.Партия КАК Ссылка,
		|				ТМЦВЭксплуатации.КоличествоОборот КАК КоличествоОборот
		|			ИЗ
		|				РегистрНакопления.ТМЦВЭксплуатации.Обороты(,,,
		|					(НЕ &ОтборОрганизация
		|							ИЛИ Организация = &Организация)
		|						И (НЕ &ОтборПодразделение
		|							ИЛИ Подразделение = &Подразделение)
		|						И (НЕ &ОтборФизическоеЛицо
		|							ИЛИ ФизическоеЛицо = &ФизическоеЛицо)
		|						И (НЕ &ОтборНоменклатура
		|							ИЛИ Номенклатура = &Номенклатура)
		|						И (НЕ &ОтборХарактеристика
		|							ИЛИ Характеристика = &Характеристика)) КАК ТМЦВЭксплуатации
		|			ГДЕ
		|				&ОтборВЭксплуатации = 1
		|	
		|			ОБЪЕДИНИТЬ ВСЕ
		|
		|			ВЫБРАТЬ
		|				ТМЦВЭксплуатации.Партия,
		|				ТМЦВЭксплуатации.Количество
		|			ИЗ
		|				РегистрНакопления.ТМЦВЭксплуатации КАК ТМЦВЭксплуатации
		|			ГДЕ
		|				&ОтборВЭксплуатации = 0
		|				И (НЕ &ОтборОрганизация
		|						ИЛИ Организация = &Организация)
		|				И (НЕ &ОтборПодразделение
		|						ИЛИ Подразделение = &Подразделение)
		|				И (НЕ &ОтборФизическоеЛицо
		|						ИЛИ ФизическоеЛицо = &ФизическоеЛицо)
		|				И (НЕ &ОтборНоменклатура
		|						ИЛИ Номенклатура = &Номенклатура)
		|				И (НЕ &ОтборХарактеристика
		|						ИЛИ Характеристика = &Характеристика)
		|	
		|			ОБЪЕДИНИТЬ ВСЕ
		|
		|			ВЫБРАТЬ
		|				ТМЦВЭксплуатации.Партия,
		|				-ТМЦВЭксплуатации.Количество
		|			ИЗ
		|				РегистрНакопления.ТМЦВЭксплуатации КАК ТМЦВЭксплуатации
		|			ГДЕ
		|				&ОтборВЭксплуатации = 1
		|				И ТМЦВЭксплуатации.Регистратор = &ТекущийРегистратор
		|				И (НЕ &ОтборОрганизация
		|					ИЛИ ТМЦВЭксплуатации.Организация = &Организация)
		|				И (НЕ &ОтборПодразделение
		|					ИЛИ ТМЦВЭксплуатации.Подразделение = &Подразделение)
		|				И (НЕ &ОтборФизическоеЛицо
		|					ИЛИ ТМЦВЭксплуатации.ФизическоеЛицо = &ФизическоеЛицо)
		|				И (НЕ &ОтборНоменклатура
		|					ИЛИ ТМЦВЭксплуатации.Номенклатура = &Номенклатура)
		|				И (НЕ &ОтборХарактеристика
		|					ИЛИ ТМЦВЭксплуатации.Характеристика = &Характеристика)
		|
		|			) КАК ВложенныйЗапрос
		|
		|		СГРУППИРОВАТЬ ПО
		|			ВложенныйЗапрос.Ссылка
		|
		|		ИМЕЮЩИЕ
		|			СУММА(ВложенныйЗапрос.КоличествоОборот) > 0
		|				ИЛИ &ОтборВЭксплуатации = 0)";
		
	Иначе
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПартииТМЦВЭксплуатацииПереопределяемый.Ссылка,
		|	ПартииТМЦВЭксплуатацииПереопределяемый.ПометкаУдаления,
		|	ПартииТМЦВЭксплуатацииПереопределяемый.Наименование,
		|	ПартииТМЦВЭксплуатацииПереопределяемый.Документ,
		|	ПартииТМЦВЭксплуатацииПереопределяемый.Дата,
		|	ПартииТМЦВЭксплуатацииПереопределяемый.КатегорияЭксплуатации,
		|	ПартииТМЦВЭксплуатацииПереопределяемый.ИнвентарныйУчет,
		|	ПартииТМЦВЭксплуатацииПереопределяемый.ИнвентарныйНомер,
		|	ПартииТМЦВЭксплуатацииПереопределяемый.СрокЭксплуатации,
		|	ПартииТМЦВЭксплуатацииПереопределяемый.ДатаЗавершенияЭксплуатации,
		|	ПартииТМЦВЭксплуатацииПереопределяемый.СпособПогашенияСтоимостиБУ,
		|	ПартииТМЦВЭксплуатацииПереопределяемый.ЕдиницаИзмеренияНаработки,
		|	ПартииТМЦВЭксплуатацииПереопределяемый.ОбъемНаработки,
		|	ПартииТМЦВЭксплуатацииПереопределяемый.СтатьяРасходов,
		|	ПартииТМЦВЭксплуатацииПереопределяемый.АналитикаРасходов,
		|	ПартииТМЦВЭксплуатацииПереопределяемый.ФизическоеЛицо,
		|	ПартииТМЦВЭксплуатацииПереопределяемый.СпособПогашенияСтоимостиНУ,
		|	ПартииТМЦВЭксплуатацииПереопределяемый.НаправлениеДеятельности,
		|	ПартииТМЦВЭксплуатацииПереопределяемый.Предопределенный,
		|	ПартииТМЦВЭксплуатацииПереопределяемый.ИмяПредопределенныхДанных
		|ИЗ
		|	Справочник.ПартииТМЦВЭксплуатации КАК ПартииТМЦВЭксплуатацииПереопределяемый
		|ГДЕ
		|	НЕ ПартииТМЦВЭксплуатацииПереопределяемый.ПометкаУдаления
		|
		|	И (НЕ &ОтборДата
		|		ИЛИ ПартииТМЦВЭксплуатацииПереопределяемый.ДатаЗавершенияЭксплуатации > &Дата
		|		ИЛИ ПартииТМЦВЭксплуатацииПереопределяемый.ДатаЗавершенияЭксплуатации = ДАТАВРЕМЯ(1, 1, 1))
		|
		|	И (&ОтборВЭксплуатации = 1
		|		И ПартииТМЦВЭксплуатацииПереопределяемый.Ссылка В
		|				(ВЫБРАТЬ
		|					ТМЦВЭксплуатации.Партия КАК Ссылка
		|				ИЗ
		|					РегистрНакопления.ТМЦВЭксплуатации.Обороты(,,, 
		|						(НЕ &ОтборОрганизация
		|								ИЛИ Организация = &Организация)
		|						И (НЕ &ОтборПодразделение
		|								ИЛИ Подразделение = &Подразделение)
		|						И (НЕ &ОтборФизическоеЛицо
		|								ИЛИ ФизическоеЛицо = &ФизическоеЛицо)
		|						И (НЕ &ОтборНоменклатура
		|								ИЛИ Номенклатура = &Номенклатура)
		|						И (НЕ &ОтборХарактеристика
		|								ИЛИ Характеристика = &Характеристика)) КАК ТМЦВЭксплуатации
		|				ГДЕ
		|					ТМЦВЭксплуатации.КоличествоОборот > 0)
		|
		|		ИЛИ &ОтборВЭксплуатации = 0
		|			И ПартииТМЦВЭксплуатацииПереопределяемый.Ссылка В
		|				(ВЫБРАТЬ
		|					ТМЦВЭксплуатации.Партия КАК Ссылка
		|				ИЗ
		|					РегистрНакопления.ТМЦВЭксплуатации КАК ТМЦВЭксплуатации
		|				ГДЕ
		|					(НЕ &ОтборОрганизация
		|							ИЛИ Организация = &Организация)
		|					И (НЕ &ОтборПодразделение
		|							ИЛИ Подразделение = &Подразделение)
		|					И (НЕ &ОтборФизическоеЛицо
		|							ИЛИ ФизическоеЛицо = &ФизическоеЛицо)
		|					И (НЕ &ОтборНоменклатура
		|							ИЛИ Номенклатура = &Номенклатура)
		|					И (НЕ &ОтборХарактеристика
		|							ИЛИ Характеристика = &Характеристика)))";
		
	КонецЕсли;
	
	СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
	СвойстваСписка.ТекстЗапроса = ТекстЗапроса;
	ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Элементы.Список, СвойстваСписка);
	
	ЗаполнитьЗначенияСвойств(ПараметрыЗапроса, Параметры);
	
	Для Каждого КлючИЗначение Из ПараметрыЗапроса Цикл
		Список.Параметры.УстановитьЗначениеПараметра(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		Список.Параметры.УстановитьЗначениеПараметра("Отбор" + КлючИЗначение.Ключ, ЗначениеЗаполнено(КлючИЗначение.Значение));
	КонецЦикла;
	
	УстановитьОтборы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборы(Форма)

	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Форма.Список,
		"ОтборВЭксплуатации",
		Форма.ОтборВЭксплуатации);

КонецПроцедуры

#КонецОбласти